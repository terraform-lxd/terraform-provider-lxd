# incus_storage_pool

Manages an Incus storage pool.

## Example Usage

### Basic Example

```hcl
resource "incus_storage_pool" "pool1" {
  name   = "mypool"
  driver = "dir"
  config = {
    source = "/var/lib/incus/storage-pools/mypool"
  }
}
```

### Cluster Example

In order to create a storage pool in a cluster, you first have to define
the storage pool on each node in the cluster. Then you can create the
actual pool

```hcl
resource "incus_storage_pool" "mypool_node1" {
  name   = "mypool"
  driver = "zfs"
  target = "node1"
}

resource "incus_storage_pool" "mypool_node2" {
  name   = "mypool"
  driver = "zfs"
  target = "node2"
}

resource "incus_storage_pool" "mypool" {
  depends_on = [
    incus_storage_pool.mypool_node1,
    incus_storage_pool.mypool_node2,
  ]

  name   = "mypool"
  driver = "zfs"
}
```

Please see the [Incus Clustering documentation](https://linuxcontainers.org/incus/docs/main/howto/cluster_config_storage/)
for more details on how to create a storage pool in clustered mode.

## Argument Reference

* `name`   - **Required** - Name of the storage pool.

* `driver` - **Required** - Storage Pool driver. Must be one of `dir`, `zfs`, `lvm`, `btrfs`, `ceph`, `cephfs`, or `cephobject`.

* `description` - *Optional* - Description of the storage pool.

* `config` - *Optional* - Map of key/value pairs of
	[storage pool config settings](https://linuxcontainers.org/incus/docs/main/reference/storage_drivers/).
	Config settings vary from driver to driver.

* `project` - *Optional* - Name of the project where the storage pool will be stored.

* `remote` - *Optional* - The remote in which the resource will be created. If
	not provided, the provider's default remote will be used.

* `target` - *Optional* - Specify a target node in a cluster.

## Importing

Import ID syntax: `[<remote>:][<project>/]<name>`

* `<remote>` - *Optional* - Remote name.
* `<project>` - *Optional* - Project name.
* `<name>` - **Required** - Storage pool name.

-> Clustered storage pools cannot be imported.

### Import example

Example using terraform import command:

```shell
$ terraform import incus_storage_pool.mypool proj/pool1
```

Example using the import block (only available in Terraform v1.5.0 and later):

```hcl
resource "incus_storage_pool" "mypool" {
  name    = "pool1"
  project = "proj"
  driver  = "zfs"
}

import {
  to = incus_storage_pool.mypool
  id = "proj/pool1"
}
```

## Notes

* The storage pool resource `config` includes some keys that can be automatically generated by the Incus.
  If these keys are not explicitly defined by the user, they will be omitted from the Terraform
  state and treated as computed values.
    - `volatile.*`
    - driver == `dir`
      + `source`
    - driver == `zfs`
      + `size`
      + `source`
      + `zfs.pool_name`
    - driver == `lvm`
      + `size`
      + `source`
      + `lvm.vg_name`
      + `lvm.thinpool_name`
    - driver == `zfs`
      + `size`
      + `source`
